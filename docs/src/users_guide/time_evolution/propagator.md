# [Computing propagators](@id doc-TE:Computing-propagators)

```@setup propagator
using QuantumToolbox
```

## Using a solver to compute a propagator

Many solvers accept an identity matrix as the initial state. When such [`QuantumObject`](@ref) is passed as the initial state, the propagator is computed. This can be used to compute a propagator for [`sesolve`](@ref), [`mesolve`](@ref), [`brmesolve`](@ref), etc. For example, 

```@example propagator
ϵ0 = 1.0 * 2π
Ω = 0.8 * 2π
Δt = π/5
H = (ϵ0/2) * sigmaz() + (Ω/2) * sigmax()
```

The propagator ``\mathcal{G}`` for the [Schrodinger equation](@ref doc-TE:Schrödinger-Equation-Solver) with a small time step ``\Delta t`` can be used as

```math
\psi(t + \Delta t) = \mathcal{G}(\Delta t) \psi(t)
```

For a Hamiltonian `H`, the corresponding propagator ``\mathcal{G}(\Delta t)`` can be generated by using [`sesolve`](@ref) with the initial state given as `qeye_like(H)`, which is the identity [`Operator`](@ref) (same as `H.type`), namely

```@example propagator
G_se = sesolve(H, qeye_like(H), [0, Δt]; progress_bar = Val(false)).states[end]
```

Thus, the state at time ``t = n \Delta t`` can be computed by applying the propagator ``\mathcal{G}(\Delta t)`` repeatedly ``n`` times on the initial state ``\psi(0)``:

```@example propagator
n = 10
ψ0 = basis(2, 0)

ψt = G_se^n * ψ0
```

Similarly, the propagator ``\mathcal{G}`` for [Lindblad master equation](@ref doc-TE:Lindblad-Master-Equation-Solver) with a small time step ``\Delta t`` can be used as

```math
|\rho(t + \Delta t)\rangle\rangle = \mathcal{G}(\Delta t) |\rho(t)\rangle\rangle
```

For a Liouvillian superoperator `L`, the corresponding propagator ``\mathcal{G}(\Delta t)`` can be generated by using [`mesolve`](@ref) with the initial state given as `qeye_like(L)`, which is the identity [`SuperOperator`](@ref) (same as `L.type`), namely

```@example propagator
L = liouvillian(H)
G_me = mesolve(L, qeye_like(L), [0, Δt]; progress_bar = Val(false)).states[end]
```

Thus, the state at time ``t = n \Delta t`` can be computed by applying the propagator ``\mathcal{G}(\Delta t)`` repeatedly ``n`` times on the initial state ``|\rho(0)\rangle\rangle``:

```@example propagator
n = 10
ρ0_vec = mat2vec(ket2dm(basis(2, 0)))

ρt_vec = G_me^n * ρ0_vec
```
